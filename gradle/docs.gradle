apply plugin: 'org.asciidoctor.convert'

task generateSite() {
  group "documentation"
  description "generates Securatta site (asciidoc, groovydoc and clover documentation)"

  dependsOn 'groovydoc'
  dependsOn 'asciidoctor'
  dependsOn 'cloverGenerateReport'

  doLast {
    copy {
      from "$buildDir/docs"
      into "$buildDir/site/groovydoc"
    }
    copy {
      from "$buildDir/asciidoc/html5"
      into "$buildDir/site"
    }
    copy {
      from "$buildDir/reports/clover/html"
      into "$buildDir/site/clover"
    }
  }
}

sourceSets {
  groovydoc {
    resources {
      srcDir folder_groovydoc
    }
  }
}

groovydoc {
  link 'https://docs.oracle.com/javase/8/docs/api/', 'java'
  link 'http://docs.groovy-lang.org/latest/html/api/', 'groovy', 'org.codehaus.groovy'
  link 'http://pacotheai.github.io/securatta/javadocs', 'securatta'

  classpath = sourceSets.groovydoc.output + classpath
  overviewText = project.resources.text.fromFile('src/docs/groovydoc/overview.html')
}

/**
 * API-DOCS
 */
ext {
	snippetsDir = file('build/generated-snippets')
}

asciidoctor {
  requires 'asciidoctor-diagram'
  sourceDir = file(folder_asciidoc)
  attributes 'source-highlighter'  :  'prettify',
             'toc'                 :  'left',
             'icons'               :  'font',
             'toclevels'           :  3,
             'snippets'            : snippetsDir,
             'releaseVersion'      : version
}
